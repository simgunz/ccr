# Maintainer: Simone Gaiarin <simgunz@gmail.com>

pkgname=mythtv
pkgver=0.26.0
pkgrel=1
epoch=1
pkgdesc="A Homebrew PVR project"
arch=('i686' 'x86_64')
url="http://www.mythtv.org/"
license=('GPL')
depends=('avahi' 'fftw' 'lame' 'libass' 'libavc1394' 'libcdio' 'libiec61883'
         'libpulse' 'libva' 'libvdpau' 'libxinerama' 'lirc-utils' 'mesa'
         'mysql-clients' 'mysql-python' 'perl-dbd-mysql' 'perl-io-socket-inet6'
         'perl-libwww' 'perl-net-upnp' 'python-lxml' 'qtwebkit' 'urlgrabber'
         'x264')
makedepends=('glew' 'libcec' 'libxml2' 'openssl' 'yasm')
optdepends=('glew: for GPU commercial flagging'
            'libcec: for consumer electronics control capabilities'
            'libxml2: to read blu-ray metadata'
            'openssl: for AirTunes (RAOP) support'
            'xmltv: to download tv listings')
conflicts=('myththemes' 'mythplugins-mythvideo')
replaces=('myththemes' 'mythplugins-mythvideo')
backup=('etc/conf.d/mythbackend')
install='mythtv.install'
source=("ftp://ftp.osuosl.org/pub/$pkgname/$pkgname-$pkgver.tar.bz2"
        'mythbackend.rc'
        'mythbackend.conf'
        'mythbackend.service'
        'mythtv.desktop'
        'mythtv-setup.desktop')
md5sums=('f57066bf75e6f14824c494d41639f4f9'
         'c8f935d42fb8617e9279bd539811ca5f'
         'ab962d83614cbd0ac11ce3fcc929829d'
         'e4d572dcc307d6d8ae26bee5aebf9f3a'
         '659ef96eeccb8559f449df4bd91cfc41'
         '347f3ddf54f884700ce15e088a01c2cd')

         
build() {
  cd "$srcdir/$pkgname-$pkgver"
  find 'bindings/python' 'contrib' -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'  

  ARCH="${CARCH/_/-}"
  ./configure --prefix=/usr \
              --cpu="$ARCH" \
              --disable-altivec \
              --disable-audio-jack \
              --disable-ccache \
              --disable-distcc \
              --enable-libfftw3 \
              --enable-libmp3lame \
              --enable-libx264 \
              --enable-vaapi \
              --python=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir" install
  
  install -D -m644 "$srcdir/mythtv.desktop" "$pkgdir/usr/share/applications/mythtv.desktop"
  install -D -m644 "$srcdir/mythtv-setup.desktop" "$pkgdir/usr/share/applications/mythtv-setup.desktop"
  install -D -m755 "$srcdir/mythbackend.rc" "$pkgdir/etc/rc.d/mythbackend"
  install -D -m644 "$srcdir/mythbackend.conf" "$pkgdir/etc/conf.d/mythbackend"
  install -D -m644 "$srcdir/mythbackend.service" "$pkgdir/usr/lib/systemd/system/mythbackend.service"
  install -D -m644 'database/mc.sql' "$pkgdir/usr/share/mythtv/mc.sql"

  mkdir -p "$pkgdir/usr/share/mythtv"
  cp -R 'contrib' "$pkgdir/usr/share/mythtv"
  mkdir -p "$pkgdir/var/log/mythtv"
}
