# Maintainer: Simone Gaiarin <simgunz@gmail.com>

pkgname=torchlight-hib
pkgver=20120926
_hibver=2012-09-26
pkgrel=1
pkgdesc='[Humble Bundle 6] A hack and slash action role-playing game'
url='http://www.torchlightgame.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('libgl' 'mesa' 'freeglut' 'libxrandr' 'expat' 'libxaw' 'libxft'
         'libxinerama' 'freeimage' 'util-linux' 'zziplib' 'pcre')
screenshot="http://www.torchlightgame.com/assets/images/torchlightlogo_small.png"
source=('torchlight-hib.desktop')
md5sums=('cb51f1a9b58d9d60dda2a3a763c7985e')

PKGEXT='.pkg.tar'
_archive="Torchlight-${_hibver}.sh"
_archive_md5='eff02cb27a818b4d29e399bc8a293d7f'

_bundle=('humblebundle6')

_humble() {
  if [[ -f "${_gamedir}/${_archive}" ]]; then
    ln -sf "${_gamedir}/${_archive}"
  fi

  if [ ! -f "${_archive}" ]; then
    for bundle in ${_bundle[@]}; do
      case ${bundle} in
        "humblebundle"*) _key="_humblebundle${bundle:12}key";;
        *) continue;;
      esac
      if [ -n "${!_key}" ]; then
        msg2 "Getting your unique ${pkgname} download location."
        _uri="$(curl -s "http://www.humblebundle.com/downloads?key=${!_key}" | grep "${_archive}" | cut -d "'" -f 10)"
        wget $_uri -O ${_archive}
        break
      fi
    done
    if [ -z "${!_key}" -a ! -f "${startdir}/${_archive}" ]; then
      error "Unable to find \"${_archive}\"."
      plain "You should run one of these commands before installing this pkg."
      for bundle in ${_bundle[@]}; do
        plain "$ export _${bundle}key=<your-${bundle}-key>"
      done
      plain "$ export _gamedir=<dir/to/${_archive}>"
      exit 1
    fi
  fi
  if ! echo "${_archive_md5}  ${_archive}" | md5sum -c --quiet; then
    echo "Invalid checksum for ${_archive}"
    return 1
  fi
  unset _key _uri _archive_md5 _bundle
}

build() {
  cd "${srcdir}"
  _humble

  chmod +x $_archive
}

package() {
  cd "${srcdir}"

  [[ -d "./temp" ]] && rm -r "./temp"
  sh "./${_archive}" --unattended --accept-license --no-register --keep --overwrite \
                     --target  "${srcdir}/temp" \
                     --bindir  "${srcdir}/bin" \
                     --datadir "${pkgdir}/opt"

  # Remove bundled libraries & helper binaries (use system packages instead)
  rm -rf "${pkgdir}/opt/Torchlight/xdg-"*

  # Fix permissions
  find "${pkgdir}" -type f -exec chmod 644 "{}" +

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  install -Dm644 "${pkgdir}/opt/Torchlight/Torchlight.png" \
                 "${pkgdir}/usr/share/pixmaps/torchlight.png"

  # Install launcher symlink
  case $CARCH in
      i686) _arch=x86;;
      x86_64) _arch=x86_64;;
  esac
  chmod 755 "${pkgdir}/opt/Torchlight/Torchlight.bin.${_arch}"
  install -d "${pkgdir}/usr/bin"
  ln -s "/opt/Torchlight/Torchlight.bin.${_arch}" "${pkgdir}/usr/bin/torchlight"

  # Install license file
  msg "Installing this package, you agree with the product license that can be found here:"
  msg    "/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "temp/config/license" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
