# Maintainer: Simone Gaiarin <simgunz@gmail.com>

pkgname=torchlight-hib
pkgver=20120926
_hibver=2012-09-26
pkgrel=1
pkgdesc='A hack and slash action role-playing game (Humble Bundle version)'
url='http://www.torchlightgame.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('libgl' 'mesa' 'freeglut' 'libxrandr' 'expat' 'libxaw' 'libxft'
         'libxinerama' 'freeimage' 'util-linux' 'zziplib' 'pcre')
screenshot="http://www.torchlightgame.com/assets/images/torchlightlogo_small.png"
source=('torchlight-hib.desktop')
md5sums=('cb51f1a9b58d9d60dda2a3a763c7985e')

_pkgmd5="eff02cb27a818b4d29e399bc8a293d7f"
_gamepkg="Torchlight-${_hibver}.sh"


package() {
  cd $srcdir

  msg "You need a full copy of this game in order to install it"
  msg "Searching for ${_gamepkg} in dir: $(readlink -f `pwd`/..)"
  if [[ -f "../${_gamepkg}" ]]; then
      msg "Found game package, installing..."
      ln -fs "../${_gamepkg}" .
  elif [[ -f "${_gamepkg}" ]]; then
      msg "Found game package in ${srcdir}, installing..."
  else
      error "Please type absolute path to ${_gamepkg} (/home/joe): "
      read pkgpath
      if [[ -f "${pkgpath}/${_gamepkg}" ]]; then
          msg "Found game package, installing..."
          ln -fs "${pkgpath}/${_gamepkg}" .
      else
          error "Unable to find game package."
          return 1
      fi
  fi

  msg "Calculating md5sum..."
  if [[ $(md5sum ${_gamepkg} | cut -d " " -f 1) != ${_pkgmd5} ]]; then
    error "One or more files did not pass the validity check!"
    return 1
  fi

  # Execute installer
  msg "Starting setup..."
  [[ -d "./temp" ]] && rm -r "./temp"
  sh "./${_gamepkg}" --unattended --accept-license --no-register --keep --overwrite \
                     --target  "${srcdir}/temp" \
                     --bindir  "${srcdir}/bin" \
                     --datadir "${pkgdir}/opt"

  # Remove bundled libraries & helper binaries (use system packages instead)
  rm -rf "${pkgdir}/opt/Torchlight/xdg-"*

  # Fix permissions
  find "${pkgdir}" -type f -exec chmod 644 "{}" +

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  install -Dm644 "${pkgdir}/opt/Torchlight/Torchlight.png" \
                 "${pkgdir}/usr/share/pixmaps/torchlight.png"

  # Install launcher symlink
  case $CARCH in
      i686) _arch=x86;;
      x86_64) _arch=x86_64;;
  esac
  chmod 755 "${pkgdir}/opt/Torchlight/Torchlight.bin.${_arch}"
  install -d "${pkgdir}/usr/bin"
  ln -s "/opt/Torchlight/Torchlight.bin.${_arch}" "${pkgdir}/usr/bin/torchlight"

  # Install license file
  msg "Installing this package, you agree with the product license that can be found here:"
  msg    "/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "temp/config/license" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
