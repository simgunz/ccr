# Maintainer: Simone Gaiarin <simgunz@gmail.com>
 
company=logmein
product=hamachi
pkgname=${company}-${product}
pkgver=2.1.0.17
pkgrel=4
pkgdesc="A zero-configuration VPN service"
arch=("i686" "x86_64")
url="https://secure.${company}.com/UK/products/${product}/"
license=("custom")
conflicts=("${product}")
replaces=("${product}")
install="${pkgname}.install"
options=("!strip")
 
[ "${CARCH}" == "i686" ] && {
  filearch='x86'
  sha1sums=('306e537c2dff541ace8bfbdd69514c12343f53d1'
	    '9b9563be0fb8de513497d1d3ca58c11cc4f52e70')
}
 
[ "${CARCH}" == "x86_64" ] && {
  filearch='x64'
  sha1sums=('fe5e311b702205a317bd598d8029d03b36d2ffec'
	    '9b9563be0fb8de513497d1d3ca58c11cc4f52e70')
}
 
source=("https://secure.${company}.com/labs/${pkgname}-${pkgver}-${filearch}.tgz"
	"hamachi-init")
 
package() {
  cd "${srcdir}/${pkgname}-${pkgver}-${filearch}"
 
  # Directories
  install -d "${pkgdir}/opt/${pkgname}/bin"
  install -d "${pkgdir}/usr/bin" "${pkgdir}/etc/rc.d"
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
 
  install -m 755 "${product}d" "${pkgdir}/opt/${pkgname}/bin"
  install -m 755 dnsup "${pkgdir}/opt/${pkgname}/bin"
  install -m 755 dnsdown "${pkgdir}/opt/${pkgname}/bin"
  install -m 755 uninstall.sh "${pkgdir}/opt/${pkgname}"
  install -m 444 README "${pkgdir}/opt/${pkgname}"
  install -m 444 LICENSE "${pkgdir}/opt/${pkgname}"
  install -m 444 CHANGES "${pkgdir}/opt/${pkgname}"
  ln -sf "/opt/${pkgname}/bin/${product}d" "${pkgdir}/usr/bin/${product}"
  install -m 755 "${srcdir}/${product}-init" "${pkgdir}/etc/rc.d/${pkgname}"
 
  # License
  ln -sf "/opt/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
 
# vim:set ts=2 sw=2 et: