# Maintainer: Simone Gaiarin <simgunz@gmail.com>

pkgname=logmein-hamachi
pkgver=2.1.0.76
pkgrel=1
pkgdesc="A zero-configuration VPN service"
arch=("i686" "x86_64")
url="https://secure.logmein.com/UK/products/hamachi/"
license=("custom")
conflicts=('hamachi')
replaces=('hamachi')
install="${pkgname}.install"
options=("!strip")

[ "${CARCH}" == "i686" ] && filearch='x86' && sha1sums=('9bb37c079d2ecaebe983affd6ed677d868804fa5'
                                                        '5766983ed25729fd020390f265645b2017e921b7'
                                                        '21ecb1e388882087205937af6455d32ee70d79e8')

[ "${CARCH}" == "x86_64" ] && filearch='x64' && sha1sums=('a0584cd3ad59582b14e53388b5883473e9371cea'
                                                          '5766983ed25729fd020390f265645b2017e921b7'
                                                          '21ecb1e388882087205937af6455d32ee70d79e8')

source=("https://secure.logmein.com/labs/${pkgname}-${pkgver}-${filearch}.tgz" 'systemd' 'logmein-hamachi.conf')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-${filearch}"

  # Directories
  install -d "${pkgdir}/opt/${pkgname}/bin"
  install -d "${pkgdir}/usr/bin"
  install -d "${pkgdir}/usr/lib/systemd/system"
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/etc/modules-load.d/"

  install -m 755 "hamachid" "${pkgdir}/opt/${pkgname}/bin"
  install -m 755 dnsup "${pkgdir}/opt/${pkgname}/bin"
  install -m 755 dnsdown "${pkgdir}/opt/${pkgname}/bin"
  install -m 755 uninstall.sh "${pkgdir}/opt/${pkgname}"
  install -m 444 README "${pkgdir}/opt/${pkgname}"
  install -m 444 LICENSE "${pkgdir}/opt/${pkgname}"
  install -m 444 CHANGES "${pkgdir}/opt/${pkgname}"
  ln -sf "/opt/${pkgname}/bin/hamachid" "${pkgdir}/usr/bin/hamachi"
  install -m 644 "${srcdir}/systemd" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -m 644 "${srcdir}/logmein-hamachi.conf" "${pkgdir}/etc/modules-load.d/logmein-hamachi.conf"

  # License
  ln -sf "/opt/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
