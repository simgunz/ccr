# Maintainer: Simone Gaiarin <simgunz@gmail.com>

pkgname=logmein-hamachi
pkgver=2.1.0.119
pkgrel=1
pkgdesc="A zero-configuration VPN service"
arch=("x86_64")
url="https://secure.logmein.com/UK/products/hamachi/"
license=("custom")
conflicts=('hamachi')
replaces=('hamachi')
install="${pkgname}.install"
options=("!strip")
source=("https://secure.logmein.com/labs/${pkgname}-${pkgver}-x64.tgz" "${pkgname}.service" 'tun.conf')
sha512sums=('c25cee2ae92f98a5eca8aa781c7f63e6e0b02c3d7fe0e659180b76ceee018aef0f2b0d90184cae3e596e5c76e2788862d5865eb0fa0c0d4eb7bfb6f8db0e604b'
            'f3451a5f9e35c3f3047ba8771163285d0fe64e869f11d7a287bd54e20e68c3f497a9462b3b2525c6a0132fedc0c88bf1517bf7709cbdf2418948ac2a0942be5c'
            '1f11e8045013648085e6268dbd2ec8cff5a3ee315d19f287f59a61e7cc61bd41800ecb89ff2c6d2a75e56cc59f530025d289f1a853b34d9129965d7ac12069e4')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-x64"

  # Directories
  install -d "${pkgdir}/opt/${pkgname}/bin"
  install -d "${pkgdir}/usr/bin"
  install -d "${pkgdir}/usr/lib/systemd/system"
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/etc/modules-load.d/"

  install -m 755 "hamachid" "${pkgdir}/opt/${pkgname}/bin"
  install -m 755 dnsup "${pkgdir}/opt/${pkgname}/bin"
  install -m 755 dnsdown "${pkgdir}/opt/${pkgname}/bin"
  install -m 444 README "${pkgdir}/opt/${pkgname}"
  install -m 444 LICENSE "${pkgdir}/opt/${pkgname}"
  install -m 444 CHANGES "${pkgdir}/opt/${pkgname}"
  ln -sf "/opt/${pkgname}/bin/hamachid" "${pkgdir}/usr/bin/hamachi"
  install -m 644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -m 644 "${srcdir}/tun.conf" "${pkgdir}/etc/modules-load.d/tun.conf"

  # License
  ln -sf "/opt/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
