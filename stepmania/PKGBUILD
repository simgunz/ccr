# Maintainer: Simone Gaiarin <simgunz@gmail.com>

pkgname=stepmania
pkgver=5.0b2a
pkgrel=1
pkgdesc="A free dance and rhythm game (was previously sm-ssc)"
url="http://www.stepmania.com/"
license="MIT"
arch=(i686 x86_64)
depends=('gtk2' 'libmad' 'mesa' 'glew' 'ffmpeg' 'libpng')
replaces=('sm-ssc')
makedepends=('pkgconfig')
install='stepmania.install'
source=('stepmania.sh'
        'stepmania.install'
        'stepmania-5.0b2a-src.tar.bz2::http://sourceforge.net/projects/stepmania/files/stepmania/5.0%20beta%202a/stepmania-5.0b2a-src.tar.bz2/download'
)
md5sums=('2bd765651419c12a5b678e62309ee3c5'
         'd98a0343a055a33311bb79be0acfb267'
         '425e37616a0a2d6f2d3f55b967e42198')

_directory="stepmania-tip"

build() {
    cd "$srcdir/$_directory"

    ./configure --disable-gtk2 --prefix="$pkgdir/opt" || return 1
    make || return 1
}

package() {
    cd "$srcdir/$_directory"

    make install
    install -D -m755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
    install -D -m755 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -D -m644 Docs/Licenses.txt "$pkgdir/usr/share/licenses/$pkgname/Licenses.txt"
    chown -R root:games icons
    cp -ar icons "$pkgdir/usr/share/"

    cd "$pkgdir/opt/"
    mv "stepmania 5" stepmania
    chown -R root:games stepmania "$pkgdir"

    # Allow users in the "games" group to copy songs, themes etc., and
    # set the sticky bit on these directories
    chmod 2775 stepmania/{Songs,Themes,NoteSkins}
}
