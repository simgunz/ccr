# Maintainer: Simone Gaiarin <simgunz@gmail.com>

pkgname=anki2
pkgver=2.0.3
pkgrel=1
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently. Version 2.0. WARNING: Do NOT install this version if you are also using the mobile version of anki. It can break your decks."
arch=('any')
url="http://ankisrs.net"
license=('AGPL3' 'BSD')
depends=('mplayer' 'pyqt' 'python2-beautifulsoup4' 'python2-httplib2')
conflicts=('anki')
optdepends=('python2-pyaudio: to record audio files')
install=anki.install
source=("http://ankisrs.net/download/mirror/anki-$pkgver.tgz")
sha256sums=('f40ee4ef29c91101cf9978ce7bd4c513f13ca7c77497a3fb50b8128adf3a5178')
sha512sums=('6ae48444701246a70f4ceee4208a8d81d9a6b559305546b4b19fd9fc47f4a5658548466d4c781c5de77fe31520238a26c9509f9fb797b87342e03aab246d6c68')

package() {
    cd "$srcdir/anki-$pkgver/"

    # remove unneeded libraries
    rm -rf libanki/thirdparty/ tools/

    sed -i 's:#!/usr/bin/env python:&2:' anki
    install -Dm755 anki "$pkgdir/usr/bin/anki"
    install -Dm644 anki.xpm "$pkgdir/usr/share/pixmaps/anki.xpm"
    install -Dm644 anki.png "$pkgdir/usr/share/pixmaps/anki.png"
    desktop-file-install anki.desktop --dir "$pkgdir/usr/share/applications/"
    install -Dm644 anki.1 "$pkgdir/usr/share/man/man1/anki.1"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 LICENSE.logo "$pkgdir/usr/share/licenses/$pkgname/LICENSE.logo"

    rm anki{,.xpm,.png,.desktop,.1,.xml} LICENSE.logo {libanki,.}/{LICENSE,ChangeLog.old} Makefile
    find -name '*.bat' -delete
    find -name 'README*' -delete

    install -d "$pkgdir/usr/share/anki/"
    cp -a * "$pkgdir/usr/share/anki/"
}
