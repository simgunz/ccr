# Maintainer: Simone Gaiarin <simgunz@gmail.com>

pkgname=redshift
pkgver=1.8
pkgrel=1
pkgdesc='Adjusts the color temperature of your screen according to your surroundings'
arch=('i686' 'x86_64')
url='http://jonls.dk/redshift/'
license=('GPL3')
depends=('libxxf86vm')
makedepends=('python2')
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2")
md5sums=('a05c34bc7b8fbb4f5d546a0b5cca5d6f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/python/python2/' src/redshift-gtk/redshift-gtk
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf -fi
  PYTHON=/usr/bin/python2 ./configure --disable-gui --disable-gnome-clock --disable-geoclue --disable-ubuntu --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
