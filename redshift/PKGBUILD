# Maintainer: Simone Gaiarin <simgunz@gmail.com>

pkgname=redshift
pkgver=1.9.1
pkgrel=1
pkgdesc='Adjusts the color temperature of your screen according to your surroundings'
arch=('i686' 'x86_64')
url='http://jonls.dk/redshift/'
license=('GPL3')
depends=('libxcb' 'libxxf86vm')
makedepends=('python3')
source=("https://github.com/jonls/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('d72a44a6eb6bf356871d5f84a759337a')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=/usr/bin/python3 ./configure --disable-gui \
                                      --disable-ubuntu \
                                      --disable-drm \
                                      --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}/usr/lib/systemd/user/${pkgname}-gtk.service"
}
