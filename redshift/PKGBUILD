# Maintainer: Simone Gaiarin <simgunz@gmail.com>

pkgname=redshift
pkgver=1.7
pkgrel=1
pkgdesc='Adjusts the color temperature of your screen according to your surroundings'
arch=('i686' 'x86_64')
url='http://jonls.dk/redshift/'
license=('GPL3')
depends=('libxxf86vm')
makedepends=('python2')
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2")
md5sums=('c56512afa292b5a94b715ed4a1841d4c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's/python/python2/' src/gtk-redshift/gtk-redshift

  PYTHON=/usr/bin/python2 ./configure --disable-gui --disable-gnome-clock --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
